plugins {
    id 'java'
    id 'jacoco'
    id 'com.vanniktech.maven.publish' version '0.30.0' apply false
}

allprojects {
    group = 'io.github.inference4j'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        jvmArgs '--enable-native-access=ALL-UNNAMED'
    }

    tasks.withType(JavaExec).configureEach {
        jvmArgs '--enable-native-access=ALL-UNNAMED'
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.16'

        testImplementation platform('org.junit:junit-bom:5.11.4')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core:3.27.3'
        testImplementation 'org.mockito:mockito-core:5.14.2'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.16'
    }
}

// Library modules included in coverage and publishing
def publishedModules = [
    'inference4j-core',
    'inference4j-runtime',
    'inference4j-spring-boot-starter',
    'inference4j-genai',
]

// Aggregate coverage report across library modules only
tasks.register('jacocoMergedReport', JacocoReport) {
    def coveredProjects = subprojects.findAll { it.name in publishedModules }
    dependsOn coveredProjects.collect { it.tasks.named('test') }

    coveredProjects.each { subproject ->
        subproject.plugins.withType(JacocoPlugin) {
            executionData.from subproject.tasks.named('test')
                    .map { it.extensions.getByType(JacocoTaskExtension).destinationFile }
            sourceDirectories.from subproject.sourceSets.main.allJava.srcDirs
            classDirectories.from subproject.sourceSets.main.output
        }
    }

    reports {
        xml.required = true
    }
}

configure(subprojects.findAll { it.name in publishedModules }) {
    apply plugin: 'com.vanniktech.maven.publish'

    mavenPublishing {
        publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL)
        signAllPublications()

        coordinates(project.group.toString(), project.name, project.version.toString())

        pom {
            name = project.name
            description = project.description
            url = 'https://github.com/inference4j/inference4j'
            inceptionYear = '2025'

            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }

            developers {
                developer {
                    id = 'viniciusccarvalho'
                    name = 'Vinicius Carvalho'
                    url = 'https://github.com/viniciusccarvalho'
                }
                developer {
                    id = 'ArthurCRodrigues'
                    name = 'Arthur Rodrigues'
                    url = 'https://github.com/ArthurCRodrigues'
                }
            }

            scm {
                connection = 'scm:git:git://github.com/inference4j/inference4j.git'
                developerConnection = 'scm:git:ssh://github.com:inference4j/inference4j.git'
                url = 'https://github.com/inference4j/inference4j'
            }
        }
    }
}
